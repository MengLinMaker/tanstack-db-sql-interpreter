/**
 * Limited SQL for Tanstack db
 * Grammar adapted from Antlr4: https://github.com/antlr/grammars-v4/tree/master/sql/sqlite
 * Alternatively refer to SQLite syntax diagrams: https://sqlite.org/syntaxdiagrams.html
 */

@top SQL {
  ignore* SELECT_STATEMENT semicolon__? ignore*
}
@skip { whitespace__ }
@precedence {
    operator @left
}

/*
 * CTE statement
 */
// WITH__ { @specialize<identifier__, 'WITH' | 'with'> }

/*
 * SELECT statement
 */
SELECT_STATEMENT {
    SELECT
    FROM
    SELECT_STATEMENT_OPTIONALS {
        WHERE?
        GROUP?
        HAVING?
        ORDER?
        LIMIT?
    }
}

/* SELECT clause */
SELECT {
    SELECT__ { @specialize<identifier__, 'SELECT' | 'select'> }
    DISTINCT__ { @specialize<identifier__, 'DISTINCT' | 'distinct'> }?
    SELECT_EXPRESSION (comma__ SELECT_EXPRESSION)*
    EXCLUDE {
        EXCLUDE__ { @specialize<identifier__, 'EXCLUDE' | 'exclude'> }
        COLUMN_NAME__ + (comma__ COLUMN_NAME__)*
    }?
}
SELECT_EXPRESSION {
    SELECT_ALL { STAR__ }
    | SELECT_TABLE { TABLE_NAME__ dot__ STAR__ }
    | SELECT_COLUMN { COLUMN }
    | SELECT_COLUMN_AS { COLUMN AS__ COLUMN_ALIAS }
    | AGGREGATE
    | SELECT_AGGREGATE_AS { AGGREGATE AS__ COLUMN_ALIAS }
}
COLUMN_ALIAS { COLUMN_NAME__ }

/* FROM clause */
FROM {
    FROM__ { @specialize<identifier__, 'FROM' | 'from'> }
    TABLE
    JOIN_EXPRESSION { JOIN_TYPE TABLE JOIN_METHOD }*
}
JOIN_TYPE { 
    JOIN { JOIN__ }
    | LEFT_JOIN { LEFT__ { @specialize<identifier__, 'LEFT' | 'left'> }  JOIN__ }
    | RIGHT_JOIN { RIGHT__ { @specialize<identifier__, 'RIGHT' | 'right'> }  JOIN__ }
    | INNER_JOIN { INNER__ { @specialize<identifier__, 'INNER' | 'inner'> }  JOIN__  }
    | FULL_JOIN { FULL__ { @specialize<identifier__, 'FULL' | 'full'> } JOIN__ }
 }
JOIN__ { @specialize<identifier__, 'JOIN' | 'join'> }
JOIN_METHOD {
    USING {
        USING__ { @specialize<identifier__, 'USING' | 'using'> }
        open_parenthesis__ COLUMN_NAME__ close_parenthesis__
    }
    | ON {
        ON__ { @specialize<identifier__, 'ON' | 'on'> }
        COLUMN EQUAL__ COLUMN
    }
}

/* WHERE clause */
WHERE {
    WHERE__ { @specialize<identifier__, 'WHERE' | 'where'> }
    EXPRESSION
}

/* GROUP clause */
GROUP {
    GROUP__ { @specialize<identifier__, 'GROUP' | 'group'> }
    BY__ COLUMN (comma__ COLUMN)*
}

/* HAVING clause */
HAVING {
    HAVING__ { @specialize<identifier__, 'HAVING' | 'having'> }
    EXPRESSION
}

/* ORDER clause */
ORDER {
    ORDER__ { @specialize<identifier__, 'ORDER' | 'order'> }
    BY__ ORDER_EXPRESSION (comma__ ORDER_EXPRESSION)*
}
ORDER_EXPRESSION {
    ORDER_COLUMN { COLUMN }
    | ORDER_ASC { COLUMN ASC__ { @specialize<identifier__, 'ASC' | 'asc'> } }
    | ORDER_DESC { COLUMN DESC__ { @specialize<identifier__, 'DESC' | 'desc'> } }
}

/* LIMIT clause */
LIMIT { 
    LIMIT_EXPRESSION { LIMIT__ NUMERIC_LITERAL__ }
    | OFFSET { LIMIT__ NUMERIC_LITERAL__ OFFSET__  NUMERIC_LITERAL__ }
}
LIMIT__ { @specialize<identifier__, 'LIMIT' | 'limit'> }
OFFSET__ { @specialize<identifier__, 'OFFSET' | 'offset'> }

/*
 * Shared grammar
 */

/* Expression */
EXPRESSION {
    open_parenthesis__ EXPRESSION close_parenthesis__
    | FUNC_SINGLE
    | FUNC_MANY
    | OPERATOR
    // | IN_ARRAY
    // | BETWEEN
    | LITERAL_VALUE
    | COLUMN
}
FUNC_SINGLE {
    UPPER { UPPER__ { @specialize<identifier__, 'UPPER' | 'upper'> } open_parenthesis__ EXPRESSION  close_parenthesis__ }
    | LOWER { LOWER__ { @specialize<identifier__, 'LOWER' | 'lower'> } open_parenthesis__ EXPRESSION  close_parenthesis__ }
    | LENGTH { LENGTH__ { @specialize<identifier__, 'LENGTH' | 'length'> } open_parenthesis__ EXPRESSION  close_parenthesis__ }
    | NOT { NOT__ { @specialize<identifier__, 'NOT' | 'not'> } EXPRESSION }
}
FUNC_MANY {
    CONCAT { CONCAT__ { @specialize<identifier__, 'CONCAT' | 'concat'> } ARRAY }
}
OPERATOR {
    EQ { EXPRESSION !operator EQUAL__ EXPRESSION }
    | NOT_EQ { EXPRESSION !operator NOT_EQ__ EXPRESSION }
    | GT { EXPRESSION !operator GT__ EXPRESSION }
    | GTE { EXPRESSION !operator GTE__ EXPRESSION }
    | LT { EXPRESSION !operator LT__ EXPRESSION }
    | LTE { EXPRESSION !operator LTE__ EXPRESSION }
    | IS { EXPRESSION !operator IS__ { @specialize<identifier__, 'IS' | 'is'> } EXPRESSION }
    | AND { EXPRESSION !operator AND__ EXPRESSION }
    | OR { EXPRESSION !operator OR__ { @specialize<identifier__, 'OR' | 'or'> } EXPRESSION }
    | LIKE { EXPRESSION !operator LIKE__ { @specialize<identifier__, 'LIKE' | 'like'> } EXPRESSION }
    | ILIKE { EXPRESSION !operator ILIKE__ { @specialize<identifier__, 'ILIKE' | 'ilike'> } EXPRESSION }
}
BETWEEN { EXPRESSION BETWEEN__ { @specialize<identifier__, 'BETWEEN' | 'between'> } EXPRESSION AND__ EXPRESSION }
IN_ARRAY { EXPRESSION IN__ { @specialize<identifier__, 'IN' | 'in'> } ARRAY }
ARRAY { open_parenthesis__ EXPRESSION (comma__ EXPRESSION)* close_parenthesis__ }
AND__ { @specialize<identifier__, 'AND' | 'and'> }

/* Aggregate */
AGGREGATE {
    COUNT { COUNT__ open_parenthesis__ COLUMN close_parenthesis__ }
    | SUM { SUM__ { @specialize<identifier__, 'SUM'> } open_parenthesis__ COLUMN close_parenthesis__ }
    | AVG { AVG__ { @specialize<identifier__, 'AVG'> } open_parenthesis__ COLUMN close_parenthesis__ }
    | MIN { MIN__ { @specialize<identifier__, 'MIN'> } open_parenthesis__ COLUMN close_parenthesis__ }
    | MAX { MAX__ { @specialize<identifier__, 'MAX'> } open_parenthesis__ COLUMN close_parenthesis__ }
    | COUNT_ALL { COUNT__ open_parenthesis__ STAR__ close_parenthesis__ }
}
COUNT__ { @specialize<identifier__, 'COUNT'> }

/* Literal */
LITERAL_VALUE {
    NUMERIC_LITERAL__
    | STRING_LITERAL__
    | NULL__
    | TRUE__ { @specialize<identifier__, 'TRUE' | 'true'> }
    | FALSE__ { @specialize<identifier__, 'FALSE' | 'false'> }
    | CURRENT_TIME__ { @specialize<identifier__, 'CURRENT_TIME' | 'current_time'> }
    | CURRENT_DATE__ { @specialize<identifier__, 'CURRENT_DATE' | 'current_date'> }
    | CURRENT_TIMESTAMP__ { @specialize<identifier__, 'CURRENT_TIMESTAMP' | 'current_timestamp'> }
}
NULL__ { @specialize<identifier__, 'NULL' | 'null'> }

/* Column and TABLE */
COLUMN {
    COLUMN_NAME__
    | TABLE_COLUMN_NAME { TABLE_NAME__ dot__ COLUMN_NAME__ }
}
TABLE {
    TABLE_NAME__
    | TABLE_ALIAS { TABLE_NAME__ TABLE_NAME__ }
}
COLUMN_NAME__ { identifier__ }
TABLE_NAME__ { identifier__ }

/* Misc */
ignore { MULTI_LINE_COMMENT__ }
AS__ { @specialize<identifier__, 'AS' | 'as'>}
BY__ { @specialize<identifier__, 'BY' | 'by'> }

@tokens {
    @precedence {
        NUMERIC_LITERAL__,
        STRING_LITERAL__,
        identifier__
    }
  
    identifier__ { $[A-Za-z0-9_]+ }
    
    /* Misc */
    dot__ { '.' }
    STAR__ { '*' }
    // PIPE__ { '||' }

    /* Syntax */
    open_parenthesis__ { '(' }
    close_parenthesis__ { ')' }
    comma__ { ',' }
    semicolon__ { ';' }

    // /* Arithmetic operator */
    // // PLUS__ { '+' }
    // // MINUS__ { '-' }
    // // DIVISION__ { '/' }
    // // MODULUS__ { '%' }

    // /* Comparison operator */
    EQUAL__ { '=' }
    NOT_EQ__ { '!=' | '<>' }
    GT__ { '>' }
    GTE__ { '>=' }
    LT__ { '<' }
    LTE__ { '<=' }

    // /* Literals */
    NUMERIC_LITERAL__ { // Numbers supported in JavaScript
        // Integer, decimals, scientific
        ('-'? $[0-9]+) ('.' $[0-9]+)? ($[eE] '-'? $[0-9]+)?
        // Hex | Octal | Binary
        | '0' $[xX] $[0-9A-F]+ | '0' $[oO] $[0-7]+ | '0' $[bB] $[0-1]+
    }
    STRING_LITERAL__ { $['] (![']|($[']$[']))+ $['] } // Wrapped in single comma `'` - use double comma inside string: 'isn''t it interesting?'

    MULTI_LINE_COMMENT__ { '/*' (![/])* '*/' } // '/' will be considered illegal force comment to close
    whitespace__ { @whitespace }
}
